#!/usr/bin/perl -w

# Convert filenames in input to fullpath, replacing with environmental
# variables too.

BEGIN { push @INC, $0 =~ s!/[^/]+$!!r; } #!

use strict;
use Getopt::Long qw(:config gnu_compat bundling require_order);
use Data::Dumper;
use MCommon;

# When invoked with "-c", process the content of the clipboard
# and put it back to the clipboard.

my $process_clipboard = 0;
GetOptions(
  "c|process-clipboard" => \$process_clipboard,
  ) or exit 1;

if ($process_clipboard) {
  system "cb | $0 | cb";
  exit 0;
}

my @envs = ();

for my $k (keys %ENV) {
  next if $k =~ /^(PWD|OLDPWD)$/x;
  my $v = $ENV{$k};
  if ($v =~ m!^/! and -d $v) {
    push @envs, [$k, $v, qr/^\Q$v/];
  }
}

sub adjuster {
  my ($k) = @_;
  return "zzzzzz$k" if $k =~ /^(IDEA_JDK)$/x; # prioritize JAVA_HOME over IDEA_JDK
  return $k;
}

@envs = sort {
    length($b->[1]) <=> length($a->[1])
    or
    adjuster($a->[0]) cmp adjuster($b->[0]);
    } @envs;

# print Dumper(@envs);

sub envify {
  my ($file) = @_;
  if ($file =~ m!^/!) {
    for my $e (@envs) {
      if ($file =~ s/$e->[2]/\$\{$e->[0]\}/) {
        return $file;
      }
    }
  }
  return $file;
}

while (defined(my $l = <>)) {
  chomp $l;
  $l =~ s!([$ENV{FILE_RE_CHARS}]+)! envify($1) !goe;
  print $l, "\n";
}

#!/usr/bin/perl -w

use strict;
use FindBin; use lib "$FindBin::Bin";
use File::Temp qw/ tempfile tempdir /;
use Getopt;
use Fcntl;

my $DEBUG = 0;

my @ARG_COLUMNS = ();
my $REREAD = 0;
my $REREAD_INTERVAL = 1;
my $SEPARATOR = ",";
my $X_COLUMN = -1;
my $Y_MIN = "*";
my $Y_MAX = "*";
my $X_MIN = "*";
my $X_MAX = "*";
my $LINE_WIDTH = 3;
my $HELP = 0;
my $BASH_COMPLETION = 0;
my $WIDTH = 1200;
my $HEIGHT = 800;
my $FONT="Noto Sans,20";
my $SHOW_ONLY = 0;
my $GRID = 1;

getopt(
    {nofiles => 0, description => "Generate a gnuput script from CVS file or STDIN."},
    ["c|columns=s", \@ARG_COLUMNS, "Select sepcified fields.  e.g. -c 2,5-6,9-"],
    ["s|separator=s", \$SEPARATOR, "Specifiy field separator.  e.g. -s ,"],
    ["r|reread", \$REREAD, "Reread data file repeatedly."],
    ["i|reread-interval=i", \$REREAD_INTERVAL, "Reread data file repeatedly. e.g. -r -i 5"],
    ["with-x-column=i", \$X_COLUMN, "Specify X column."],
    ["y-min=i", \$Y_MIN, "Specify Y range min."],
    ["y-max=i", \$Y_MAX, "Specify Y range max."],
    ["x-min=i", \$X_MIN, "Specify X range min."],
    ["x-max=i", \$X_MAX, "Specify X range max."],
    ["width=i", \$WIDTH, "Specify window width."],
    ["height=i", \$HEIGHT, "Specify window height."],
    ["font=s", \$FONT, "Specify font in 'name(,size)' format."],
    ["line-width=i", \$LINE_WIDTH, "Specify line width in pixels."],
    ["grid!", \$GRID, "With grid (default); or use --no-grid."],
    ["p|print-script", \$SHOW_ONLY, "Only print generated script; don't run."],
    ["v|verbose", \$DEBUG, "Enable verbose output."],
    );

# Config
my @GNUPLOT = qw(gnuplot );

my $TEMPLATE = <<EOF; # TODO Escape...
set terminal wxt persist noraise enhanced linewidth $LINE_WIDTH size $WIDTH,$HEIGHT font "$FONT"

set datafile separator "$SEPARATOR"

set yrange [ $Y_MIN : $Y_MAX]
set xrange [ $X_MIN : $X_MAX]

EOF

# Helpers.

sub min($$) {
  return $_[0] <= $_[1] ? $_[0] : $_[1];
}

sub build_columns {
  my ($num_columns, @arg_columns) = @_;
  my @ret = ();

  if (@arg_columns == 0) {
    print "## All columns: 1..$num_columns\n" if $DEBUG;
    push(@ret, 0 .. ($num_columns - 1));
  } else {
    for my $arg (@arg_columns) {
      for my $token (split /\s*\,\s*/, $arg) {
        print "## Token: $token\n" if $DEBUG;
        my ($single, $start, $end) = ();

        if ($token =~ /^ (?: (\d+) | (\d+)? \- (\d+)? ) $/x) {
          ($single, $start, $end) = ($1, $2, $3);
        }
        if (defined $single && ($single >= 1)) {
          # -c N
          push @ret, min($single, $num_columns) - 1;
          next;
        }
        if (defined $start or defined $end) {
          # -c M-N
          $start //= 1;
          $start = min($start, $num_columns);

          $end //= $num_columns;
          $end = min($end, $num_columns);
          print "### $start - $end\n" if $DEBUG;
          if ($start <= $end) {
            push @ret, ($start - 1) .. ($end - 1);
            next;
          }
        }
        die "$0: Invalid columns specified: '$token'\n";
      }
    }
  }
  return @ret;
}

sub remove_cloexec {
  for my $fh (@_) {
    my $flags = fcntl($fh, F_GETFD, 0) or die "fcntl F_GETFD: $!";
    fcntl ($fh, F_SETFD, $flags & ~FD_CLOEXEC) or die "fcntl F_SETFD: $!";
  }
}

# Start
sub do_file {
  my ($file, $in) = @_;

  # Parse the header and build the column selection.
  defined(my $head = <$in>) or die "$0: Cannot read header from '$file': file empty\n";
  $head =~ s!^\s+!!;
  $head =~ s!\s+$!!;

  my @column_names = split(/\s*\Q$SEPARATOR\E\s*/o, $head);
  print "# Columns: ", join(",", @column_names), "\n" if $DEBUG;

  my @columns = build_columns(scalar(@column_names), @ARG_COLUMNS);
  print "# Selection: ", join(",", @columns), "\n" if $DEBUG;

  # Generate the script.
  my $x_spec = $X_COLUMN >= 0 ? "$X_COLUMN:" : "";

  my ($scripth, $script_name) = tempfile(UNLINK => 1);

  print "# Script: $script_name\n" if $DEBUG;
  print $scripth $TEMPLATE; # TODO Escape it.
  print $scripth "set grid\n" if $GRID;
  print $scripth "plot \\\n";
  for my $column (@columns) {
    printf $scripth ("    '%s' using %s%d with lines title \"%s\",\\\n",
        $file, $x_spec, $column + 1, $column_names[$column]);
  }
  printf $scripth "\n";
  if ($REREAD) {
    printf $scripth "pause $REREAD_INTERVAL\nreread\n";
  }
  # Seek back to the start.
  seek $scripth, 0, 0 or die "$0: Seek failed: $!\n";

  if ($DEBUG) {
    while (<$scripth>) {
      print;
    }
    seek $scripth, 0, 0 or die "$0: Seek failed: $!\n";
  }

  if (!$SHOW_ONLY) {
    # Remove close-on-exec and remove the file, and pass the FD instead.
    remove_cloexec $scripth;
    my $script_fd = fileno($scripth);

    # exec qw(ls -l /dev/fd/);
    exec (@GNUPLOT, "/dev/fd/$script_fd");
  }
}

sub prepare_to_read_from_stdin {
  my ($th, $temp_name) = tempfile(UNLINK => 1);
  my $tfd = fileno($th);
  remove_cloexec($th);

  print "# Copying stdin to $temp_name (/dev/fd/$tfd)\n" if $DEBUG;

  # Copy the header and the first line to the temp file.
  # Without the first data line, gnuplot won't start.
  defined(my $head = <>) or die "$0: Cannot read header: stdin is empty.\n";
  print $th $head;
  print "# Header: $head" if $DEBUG;

  defined(my $line = <>) or die "$0: Cannot read first line: EOF\n";
  print $th $line;

  flush $th;

  # Let a child copy the rest.
  my $cid = fork();
  if ($cid == 0) {
    # Child
    while (<>) {
      print $th $_;
      flush $th;
    }
    exit 0;
  }
  # close STDIN;
  return ("/dev/fd/$tfd");
}

sub main {
  my @FILES = @_;

  if (@FILES == 0) {
    @FILES = prepare_to_read_from_stdin();
  }

  for my $file (@FILES) {
    # Open the file.
    print "# Opening $file\n" if $DEBUG;
    open(my $in, "<", $file) or die "$0: Cannot open '$file': $!\n";

    # Make sure it's seekable.  Otherwise gnuplot won't work.
    seek($in, 0, 0) or die "$0: File '$file' is not seekable; $!\n";

    do_file $file, $in;
  }
}

main @ARGV;
